in vec3 position;
in vec2 texcoord;
in vec4 color;

uniform highp mat4 modelMatrix;
uniform highp mat4 viewMatrix;
uniform highp mat4 projMatrix;
uniform highp mat4 extraMatrix;
uniform highp vec3 cameraPos;
uniform highp vec3 actorPos;
uniform bool textured;
uniform bool isBillboard;
uniform bool inOverworld;

out vec2 Texcoord;
out vec4 Color;

void main()
{
	vec4 pos = vec4(position, 1.0);
	if (inOverworld) {
		pos = viewMatrix * extraMatrix * pos;
		pos += vec4(pos.w * actorPos, 0.0);
	} else if (isBillboard) {
		vec4 offset = modelMatrix * vec4(0.0, 0.0, 0.0, 1.0);
		offset -= vec4(cameraPos * offset.w, 0.0);
		offset = viewMatrix * offset;

		pos = extraMatrix * pos;
		pos += vec4(offset.xyz * pos.w, 0.0);
	} else {
		pos = modelMatrix * pos;
		pos -= vec4(cameraPos * pos.w, 0.0);
		pos = viewMatrix * pos;
	}

	pos /= pos.w;
	pos.z *= -1.0;
	
	pos = projMatrix * pos;
	if (isBillboard)
		pos.z = round(pos.z);

	gl_Position = pos;
	
	Color = color;
	if (textured) {
		Texcoord = texcoord;
	} else {
		Texcoord = vec2(0.0, 0.0);
	}
}
